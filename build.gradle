plugins {
    id 'java-library'
    id 'groovy'

    id 'maven-publish'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

group = 'me.jamiemansfield'
version = '0.3.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    api 'me.jamiemansfield:string:0.1.0'

    testImplementation 'org.codehaus.groovy:groovy-all:3.0.7'
    testImplementation 'org.spockframework:spock-core:2.0-M4-groovy-3.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
}

test {
    useJUnitPlatform()
}

processResources {
    from 'LICENSE.txt'
}

javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.links(
            'https://docs.oracle.com/javase/8/docs/api/'
    )
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.archivesBaseName
            from components.java

            pom {
                name = project.name
                description = project.description
                packaging = 'jar'
                url = project.url
                inceptionYear = project.inceptionYear

                scm {
                    url = 'https://github.com/jamiemansfield/csv'
                    connection = 'scm:git:https://github.com/jamiemansfield/csv.git'
                    developerConnection = 'scm:git:git@github.com:jamiemansfield/csv.git'
                }

                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/jamiemansfield/csv/issues'
                }

                licenses {
                    license {
                        name = 'BSD 3-Clause'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'jamierocks'
                        name = 'Jamie Mansfield'
                        email = 'jmansfield@cadixdev.org'
                        url = 'https://www.jamiemansfield.me/'
                        timezone = 'Europe/London'
                    }
                }
            }
        }
    }
    repositories {
        if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
            maven {
                url = !version.endsWith('-SNAPSHOT') ?
                        'https://oss.sonatype.org/service/local/staging/deploy/maven2/' :
                        'https://oss.sonatype.org/content/repositories/snapshots/'

                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
    apply plugin: 'signing'
    signing {
        required { !version.endsWith('-SNAPSHOT') && gradle.taskGraph.hasTask(tasks.publish) }
        sign publishing.publications.mavenJava
    }
}
